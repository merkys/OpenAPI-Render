#!/usr/bin/perl

use strict;
use warnings;

use CGI qw(:standard -nosticky -utf8 start_div end_div);
use JSON;
use OpenAPI::View qw(dereference);

my $filename = shift @ARGV;

open( my $inp, $filename );
my $api = decode_json( join '', <$inp> );
close $inp;

$api = dereference( $api, $api );

print header( -type => 'text/html',
              -expires => 'now',
              -charset => 'UTF-8' ),
      start_html( -title => $api->{info}{title} . ' v' .
                            $api->{info}{version} );

my( $base_url ) = map { $_->{url} } @{$api->{servers} };

for my $path (sort keys %{$api->{paths}}) {
    print h1( $path );
    for my $operation ('get', 'post', 'patch', 'put', 'delete') {
        next if !$api->{paths}{$path}{$operation};
        my @parameters = (
            exists $api->{paths}{$path}{parameters}
               ? @{$api->{paths}{$path}{parameters}} : (),
            exists $api->{paths}{$path}{$operation}{parameters}
               ? @{$api->{paths}{$path}{$operation}{parameters}} : () );
        my @fields;
        for my $parameter (@parameters) {
            next if $parameter->{'$ref'}; # Will have to dereference later
            push @fields,
                 h3( $parameter->{name} ),
                 $parameter->{description} ? p( $parameter->{description} ) : (),
                 input( { -type => 'text',
                          -name => $parameter->{name},
                          (exists $parameter->{example}
                            ? ( -placeholder => $parameter->{example} )
                            : ()) } );
        }
        print start_form( -action => $base_url . $path,
                          -method => $operation ),
              fieldset(
                legend( uc( $operation ) .
                        ( $api->{paths}{$path}{$operation}{description}
                             ? ': ' . $api->{paths}{$path}{$operation}{description} : '' ) ),
                @fields,
                submit ),
              end_form;
    }
}

print end_html,
      "\n";
